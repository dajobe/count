#!/usr/bin/perl -w
#
# $Source: /home/dajobe/dev/count/repo/count/count,v $
#
# $Id: count,v 1.7 2003-08-21 11:25:36 cmdjb Exp $
#
# USAGE:
#
#   count [--min=NUM] [--histogram] <field-ranges> [files...]
#
# where <field-ranges> is list of comma-separated ranges of fields of the form 
# field1-field2, field or '-' for the entire entry. e.g.:
#
#  count 0,3,5-7 file
#  count 0,1
#  count 2,- file1 file2
#  count 5 -
#  count 5
#  (last two are equivalent)
#
#
# If --min=NUM is given, only results with MIN or more counts are displayed
# the rest are summarised in one item.
#
# Count the entries in the given whitespace-separated field (from 0)
# in the given files and report the results, sorted with most populal
# first in order with percentages and totals.
#
# (C) Copyright 1996-2003 Dave Beckett <D.J.Beckett@bristol.ac.uk>
#

use strict;

use Carp;

warn <<"EOT" and exit 0 if !@ARGV;
Usage:
  count [--min=NUM] [--histogram] <field-ranges> [files...]
where
  <field-ranges> is field1-field2, field or '-' for entire line
     examples: 0,3,5-7  0,1 2 5
  files can include '-' for stdin or assumes '-' if no files given.

  --min=NUM    restricts the minimum count to show and summarises the rest
  --histogram  prints histogram of the percentages with '#' symbols
EOT

&::main(@ARGV);

exit 0;


sub main {
  my(@args)=@_;

  my $min=0;
  my $histogram=0;

  my $HIST_WIDTH=60;

  while($args[0] =~ /^--(\w+)=(\w+)$/) {
    my($key,$val)=($1,$2);
    if($key eq 'min') {
      if($val =~ /^\d+/ && $val > 0) {
	$min=$val;
      }
    } else {
      croak "I don't understand $args[0]\n";
    }
    shift(@args);
  }
  while($args[0] =~ /^--(\w+)$/) {
    my $key=$1;
    if($key eq 'histogram') {
      $histogram=1;
    } else {
      croak "I don't understand $args[0]\n";
    }
    shift(@args);
  }

  my($field_nos, @files)=@args;

  @files='-' if !@files;

  # Parse fields arg to get ranges
  my(@fields);
  {
    # Build list of fields without duplicates
    my(%fields_seen);
    foreach (split (/,/, $field_nos)) {
      if (/^(\d+)-(\d+)$/) {
	foreach ($1..$2) {
	  push(@fields, $_), $fields_seen{$_}=1 if !$fields_seen{$_};
	}
      } elsif (/^(-|\d+)$/) {
	push(@fields, $_), $fields_seen{$_}=1 if !$fields_seen{$_};
      } else {
	croak "I don't understand range '$_'\n";
      }
    }
  }

  my %db;
  my %totalc;
  # Init value lengths
  my %max_valen=map {$_ => 0} @fields; # B5 joke

  my $file;
  foreach $file (@files) {
    open (::IN, $file) or carp "Cannot open $file - $!\n", next;
    
    while(<IN>) {
      chop;
      my $field;
      foreach $field (@fields) {
	my $value=($field eq '-') ? $_ : (split(/\s+/))[$field];

	next if !defined $value;

	$db{$field}{$value}++; $totalc{$field}++;

	$max_valen{$field}=length($value) if length($value) > $max_valen{$field};
      }
    }
    close(IN);
  }

  # Build count lengths
  my %max_clen;
  {
    my $field;
    foreach $field (@fields) {
      $max_clen{$field}=0;
      my $db_ref=$db{$field};
      if ($db_ref) {
	foreach (values %$db_ref) {
	  $max_clen{$field}=length($_) if length($_) > $max_clen{$field}
	}
      }
    }
  }


  # Output results
  {
    my $field;
    foreach $field (@fields) {
      my $db_ref=$db{$field};

      print "Summary for field $field\n\n";

      my(@entry_order);
      if ($db_ref) {
	@entry_order=sort {
	  $db_ref->{$::b} <=> $db_ref->{$::a}
	} keys %$db_ref;
      }

      my $this_max_valen=$max_valen{$field} || 0;
      my $this_max_clen=$max_clen{$field} || 0;
      my $this_totalc=$totalc{$field} || 0;

      my $Total_Label='Total';
      $this_max_valen=length($Total_Label) if length($Total_Label) > $this_max_valen;
      $this_max_clen=length($this_totalc) if length($this_totalc) > $this_max_clen;

      my $format_str="%-${this_max_valen}s %${this_max_clen}d %6.2f%%";
      $format_str .= " %s" if $histogram;
      $format_str .= "\n";
	
      my $rest_count=0;
      my $rest_entry_count=0;

      my $entry;
      foreach $entry (@entry_order) {
	my $count=$db_ref->{$entry};

	if($count < $min) {
	  $rest_count+= $count;
	  $rest_entry_count++;
	  next;
	}

	my $pcent=int(10000*$count/$this_totalc)/100;
	my $hist='#' x ($HIST_WIDTH*$pcent/100);
	print sprintf($format_str, $entry, $count, $pcent, $hist);
      }
      
      if($rest_count) {
	my $pcent=int(10000*$rest_count/$this_totalc)/100;
	my $hist='#' x ($HIST_WIDTH*$pcent/100);
	print sprintf($format_str, "remaining $rest_entry_count items", $rest_count, $pcent, $hist);
      }

      print "-" x $this_max_valen . " " . "-" x $this_max_clen . " -------\n";
      my $hist='#' x $HIST_WIDTH;
      print sprintf($format_str, "", $this_totalc, 100.00, $hist);
    }
  }
}

# Local Variables:
# mode: perl
# End:
